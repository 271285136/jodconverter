description = 'JODConverter - Cli'

dependencies {
    implementation project(':jodconverter-local')
    implementation project(':jodconverter-online')

    implementation libraries.commons_cli
    implementation libraries.commons_io
    implementation libraries.commons_lang3
    implementation libraries.spring_core
    implementation libraries.spring_context

    runtimeOnly libraries.slf4j_log4j // Runtime so it is included in the distribution

    testImplementation libraries.wiremock
}

// Create an executable for the client module
// todo: include documentation with the distibution
// See https://docs.gradle.org/current/userguide/application_plugin.html
apply plugin: 'application'

mainClassName = "org.jodconverter.cli.Convert"

// Copy the conf folder into the distribution
applicationDistribution.from("conf/") {
    into "conf"
}

// Copy the README
applicationDistribution.from("README.txt") {
    into ""
}

// use the log4j.properties from the configuration directory
applicationDefaultJvmArgs = ["-Dlog4j.configuration=file:MY_APP_HOME/conf/log4j.properties"]
startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('MY_APP_HOME', '$APP_HOME')
        windowsScript.text = windowsScript.text.replace('MY_APP_HOME', '%APP_HOME%')
    }
}