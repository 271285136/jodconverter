// Import the content of the libraries.gradle file
apply from: "./libraries.gradle"

description = 'JODConverter'

// External dependencies for the build script
buildscript {

  repositories {
    mavenCentral()

    //Needed only for SNAPSHOT versions
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://plugins.gradle.org/m2" }
  }

  dependencies {
    classpath "com.netflix.nebula:nebula-project-plugin:$nebulaProjectVersion"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotlessVersion"
    classpath "io.spring.gradle:dependency-management-plugin:$springDependencyManagementVersion"
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:$nexusStagingVersion"
    classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:$coverallsVersion"
    classpath "org.standardout:gradle-versioneye-plugin:$versioneyeVersion"
  }
}

ext {
  baselineJavaVersion = JavaVersion.VERSION_1_7
  sourceEncoding = "UTF-8"

  ext.releaseBuild = version.endsWith('RELEASE')
  ext.snapshotBuild = version.endsWith('SNAPSHOT')

  // Set up different subproject lists for individual configuration
  ext.javaprojects = subprojects.findAll { new File(it.projectDir, 'src/main/java').directory }
  ext.publishprojects = [project(':jodconverter-core'), project(':jodconverter-spring'), project(':jodconverter-spring-boot-starter')]
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Configuration to apply to all the projects

allprojects {

  // Add the clean task to all the project
  apply plugin: 'base'
  // Generates files for Intellij IDEA
  apply plugin: 'idea'
  // Generates files for Eclipse
  apply plugin: 'eclipse'
  // Generate reports containing useful information about the build (tasks, dependencies, properties)
  apply plugin: 'project-report'

  // Set the group and current version
  group = groupId
  version = currentVersion

  // Eclipse setup

  // Workaround until https://github.com/gradle/gradle/issues/898
  // is resolved.

  // Several files have UTF-8 encoding and Eclipse running on Windows
  // will have trouble unless we tell it to use UTF-8 encoding. This
  // setting needs to go into the org.eclipse.core.resources.prefs file,
  // which the project script isn't set up to configure.
  eclipse {

    ext.corePrefsPath = '.settings/org.eclipse.core.resources.prefs'

    ext.genCorePrefs = {
      File corePrefs = file(corePrefsPath)
      corePrefs.parentFile.mkdirs()
      corePrefs.text = """\
      eclipse.preferences.version=1
      encoding/<project>=UTF-8
      """.stripIndent()
    }

    project {
      file {
        // Will be applied when the project is imported into Eclipse
        genCorePrefs()
      }
    }

    // Will be applied after the eclipseProject task
    eclipseProject.doLast {
      genCorePrefs()
    }

    // Will be applied after the cleanEclipse task
    cleanEclipse.doLast {
      delete corePrefsPath
    }
  }

  tasks.eclipse.dependsOn(cleanEclipse)
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Java projects configuration

// Apply the configuration for the java projects only
configure(javaprojects) {

  // Spotless need a buildscript repository for all java project
  buildscript {
    repositories {
      maven { url "https://plugins.gradle.org/m2" }
    }
  }

  // Apply the gradle file specific to all java projects
  apply from: "$rootDir/gradle/java-projects.gradle"
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Code quality control coverage (JaCoCo)

// We need JacocoMerge to aggregate sub projects report
apply plugin: 'jacoco'
// Send coverage data to coveralls.io
apply plugin: 'com.github.kt3k.coveralls'

// Configures the JaCoCo plugin
jacoco {
  toolVersion = jacocoVersion
}

// Creates an aggregated JaCoCo executionData for all
// the tests (unit tests and integration tests) found
// in sub Java projects 
task jacocoTestMerge(type: JacocoMerge) {
    description = 'Aggregates JaCoCo test and integration test coverage reports of all projects.'
    group = "Reporting"
    
    // Gather execution data from all subprojects
    // (change this if we want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    destinationFile file("$rootDir/build/jacoco/overallCoverage.exec")
}
project.tasks["jacocoTestMerge"].dependsOn javaprojects*.test, javaprojects*.integrationTest

// Code coverage report for all the sub (java) projects
task jacocoTestReport(type: JacocoReport, dependsOn: jacocoTestMerge) {
    description = 'Aggregates JaCoCo coverage reports of all projects.'
    group = "Reporting"
    
    // Gather merged execution data from all subprojects
    executionData "${buildDir.name}/jacoco/overallCoverage.exec"

    // Add all relevant sourcesets from the subprojects 
    javaprojects.each {
         sourceSets it.sourceSets.main
         //sourceSets it.sourceSets.test
    }

    reports {
        xml.enabled true // coveralls plugin depends on xml format report
        html.enabled true
    }
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Publishing configuration

// Add the ability to release to Maven Central from Gradle without dealing with Nexus UI
apply plugin: "io.codearte.nexus-staging"

nexusStaging {
  username = findProperty('ossrhUsername')
  password = findProperty('ossrhPassword')
}

// Apply the configuration for the published projects only
configure(publishprojects) {

  // Apply the gradle file specific to all published projects
  apply from: "$rootDir/gradle/publish-projects.gradle"
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Distribution configuration

task distZip(type: Zip) {
  description "Create full distribution zip"
  baseName project.name
  group "Distribution"
   
  //from subprojects.collect { it.tasks.matching { it.name == 'distZip' || it.name == 'distTar' } }
  from subprojects.collect { it.distZip }
}
project.tasks["distZip"].dependsOn subprojects*.distZip
//project.tasks["distZip"].dependsOn subprojects*.assembleDist

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Versioneye configuration

// Update project's dependencies status on http://www.versioneye.com
apply plugin: 'org.standardout.versioneye'

versioneye {
  includePlugins = false
  includeSubProjects = true
  exclude 'testCompile', 'testCompileClasspath', 'testCompileOnly', 'testImplementation', 'testRuntime', 'testRuntimeClasspath', 'testRuntimeOnly', 'integTestCompile', 'integTestCompileClasspath', 'integTestCompileOnly', 'integTestImplementation', 'integTestRuntime', 'integTestRuntimeClasspath', 'integTestRuntimeOnly', 'checkstyle', 'jacocoAgent', 'jacocoAnt', 'pmd'
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Install a wrapper in the current projet (gradlew)
task wrapper(type: Wrapper) {
  description = 'Generates gradlew[.bat] scripts'
  group = "wrapper"

  gradleVersion = '4.0'
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
